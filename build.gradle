plugins {
	id 'org.springframework.boot' version '2.4.3'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
        id "com.github.ben-manes.versions" version "0.38.0"
        id 'jacoco'
}

group = 'training'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '8'

repositories {
	mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}


dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.zalando:problem:0.25.0'
    implementation 'org.zalando:jackson-datatype-problem:0.25.0'
    implementation 'org.springdoc:springdoc-openapi-ui:1.5.6'
    implementation 'org.glassfish.jaxb:jaxb-runtime'
    implementation 'org.liquibase:liquibase-core'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    implementation 'org.mapstruct:mapstruct:1.4.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.4.2.Final'

    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'com.h2database:h2'

    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.mockito:mockito-core'
    testImplementation 'org.mockito:mockito-junit-jupiter'
    testImplementation 'org.assertj:assertj-core'

    integrationTestImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}

    integrationTestImplementation 'io.rest-assured:json-path'
    integrationTestImplementation 'io.rest-assured:json-schema-validator'
    integrationTestImplementation 'io.rest-assured:rest-assured'
    integrationTestImplementation 'io.rest-assured:spring-mock-mvc'

}

test {
	useJUnitPlatform()
	finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    useJUnitPlatform()

    systemProperty 'spring.datasource.url', findProperty('spring.datasource.url') ?: "jdbc:h2:mem:db;DB_CLOSE_DELAY=-1"
    systemProperty 'spring.datasource.username', findProperty('spring.datasource.username') ?: "su"
    systemProperty 'spring.datasource.password', findProperty('spring.datasource.password') ?: "su"

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    shouldRunAfter test
    finalizedBy jacocoTestReport
}

check.dependsOn integrationTest

jacocoTestReport {

    reports {
       xml.enabled true
       csv.enabled true
   }
   getExecutionData().setFrom(fileTree(buildDir).include("/jacoco/*.exec"))
}
